plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'org.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

bootJar {
    enabled = true
    archiveFileName = "server.jar"
}

jar {
    enabled = false
}

dependencies {
    // ======== Spring Boot Core ========
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // ======== Database ========
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'

    // ======== Developer Tools ========
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // ======== API Documentation ========
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.5'

    // ======== Security (JWT) ========
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'

    // ======== External Integrations & Utilities ========
    implementation "software.amazon.awssdk:s3:2.25.0"
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.9.3'
    implementation 'org.jsoup:jsoup:1.17.2'

    // ======== Monitoring & Logging ========
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'ch.qos.logback.contrib:logback-jackson:0.1.5'
    implementation 'ch.qos.logback.contrib:logback-json-classic:0.1.5'
    implementation 'net.logstash.logback:logstash-logback-encoder:8.1'

    // ======== Testing ========
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // ======== Image Type Converting ========
    implementation 'com.sksamuel.scrimage:scrimage-core:4.1.3'
    implementation 'com.sksamuel.scrimage:scrimage-webp:4.1.3'
}

tasks.named('test') {
    useJUnitPlatform()
}
